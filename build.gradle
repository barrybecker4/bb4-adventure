// Copyright by Barry G. Becker, 2016 - 2018. Licensed under MIT License: http://www.opensource.org/licenses/MIT
plugins {
    id "com.github.maiflai.scalatest" version "0.19" // needed to run scala tests
}
description = 'A framework for creating adventure games. It could also be used for other educational purposes.'
group = 'com.barrybecker4'
version = '1.5-SNAPSHOT'
apply from: 'https://raw.githubusercontent.com/bb4/bb4-common/master/bb4.gradle'

project.archivesBaseName = 'bb4-adventure'
mainClassName = "com.barrybecker4.puzzle.adventure.ui.GraphicalAdventure"


dependencies {
    compile 'com.barrybecker4:bb4-sound:1.5-SNAPSHOT'
    compile 'com.barrybecker4:bb4-ui:1.5-SNAPSHOT'
    // Used to generate html unit test report
    testRuntime 'org.pegdown:pegdown:1.6.0'
}

task runAdventure(type: JavaExec) {
    group = 'application'
    main = 'com.barrybecker4.puzzle.adventure.ui.GraphicalAdventure'
    classpath = sourceSets.main.runtimeClasspath
}

task runTextAdventure(type: JavaExec) {
    standardInput = System.in
    group = 'application'
    main = 'com.barrybecker4.puzzle.adventure.TextAdventure'
    classpath = sourceSets.main.runtimeClasspath
    args = ['ludlow/ludlowScript.xml']
}

task runTextBinaryTutorial(type: JavaExec) {
    standardInput = System.in
    group = 'application'
    main = 'com.barrybecker4.puzzle.adventure.TextAdventure'
    classpath = sourceSets.main.runtimeClasspath
    args = ['learnBinary/binaryScript.xml']
}

task runBinaryTutorial(type: JavaExec) {
    group = 'application'
    main = 'com.barrybecker4.puzzle.adventure.ui.GraphicalAdventure'
    classpath = sourceSets.main.runtimeClasspath
    args = ['learnBinary/binaryScript.xml']
}

ext.appMap = new LinkedHashMap()
appMap.put('adventure',
    [include:['com/barrybecker4/puzzle/adventure/**'],
     exclude:['com/barrybecker4/puzzle/adventure/**/*.pdf']])


// The application is deployed to /build/distributions. Run scripts are in the bin directory.
// Additional script to run text version.
// It can take parameters like 'ludlow/ludlowScript.xml' or 'learnBinary/binaryScript.xml'
task createTextAdventureApp(type: CreateStartScripts) {
    mainClassName = "com.barrybecker4.puzzle.adventure.TextAdventure"
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
    applicationName = 'bb4-text-adventure'
    defaultJvmOpts = ["-Xmx1024m"]
}

// add extra execution scripts
applicationDistribution.into("bin") {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(createTextAdventureApp)
    fileMode = 0755
}


apply from: 'https://raw.githubusercontent.com/bb4/bb4-common/master/bb4-publish.gradle'
